apply plugin: 'com.android.application'

//***********************************************************************************************//
def IOTHUB_E2E_X509_PRIVATE_KEY_BASE64 = project.hasProperty('IotHubPrivateKeyBase64') ? '"'+project.property('IotHubPrivateKeyBase64')+'"' : '"Define IotHub Private Key"'
def IOTHUB_E2E_X509_CERT_BASE64 = project.hasProperty('IotHubPublicCertBase64') ? '"'+project.property('IotHubPublicCertBase64')+'"': '"Define IotHub Public Cert"'
def IOTHUB_CONNECTION_STRING_ENV_VAR_NAME = project.hasProperty('IotHubConnectionString') ? '"'+project.property('IotHubConnectionString')+'"' : '"Define IotHub Connection"'
def IOTHUB_E2E_X509_THUMBPRINT = project.hasProperty('IotHubThumbprint') ? '"'+project.property('IotHubThumbprint')+'"': '"Define IotHub Cert Thumbprint"'
def IOTHUB_CONN_STRING_INVALIDCERT = project.hasProperty('IotHubInvalidCertConnectionString') ? '"'+project.property('IotHubInvalidCertConnectionString')+'"': '"Define IotHub Invalid Cert Connection"'
def APPCENTER_APP_SECRET = project.hasProperty('AppCenterAppSecret') ? '"'+project.property('AppCenterAppSecret')+'"': '"Define AppCenter App Secret"'

def STRING='String'
//***********************************************************************************************//

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.iothub.azure.microsoft.com.androide2e"
        minSdkVersion 24
        targetSdkVersion 27
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //********** We can define variables here **********
        each {
            //buildTypes.mBuildConfigFields 'DATATYPE','VARIABLE',|"GRADLE VARIABLE|"'
            buildConfigField STRING, 'IotHubPrivateKeyBase64', IOTHUB_E2E_X509_PRIVATE_KEY_BASE64
            buildConfigField STRING, 'IotHubPublicCertBase64', IOTHUB_E2E_X509_CERT_BASE64
            buildConfigField STRING, 'IotHubConnectionString', IOTHUB_CONNECTION_STRING_ENV_VAR_NAME
            buildConfigField STRING, 'IotHubThumbprint', IOTHUB_E2E_X509_THUMBPRINT
            buildConfigField STRING, 'IotHubInvalidCertConnectionString', IOTHUB_CONN_STRING_INVALIDCERT
            buildConfigField STRING, 'AppCenterAppSecret', APPCENTER_APP_SECRET
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    packagingOptions {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'thirdpartynotice.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions{
        ignore 'InvalidPackage'
    }
}

dependencies {
    def appCenterSdkVersion = '1.8.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:multidex:1.0.3'

    implementation ('com.microsoft.azure.sdk.iot:iot-e2e-common:0.20.0'){
        exclude module: 'junit'
        exclude module: 'azure-storage'
    }
    implementation ('com.microsoft.azure.android:azure-storage-android:2.0.0@aar')

    androidTestImplementation 'com.microsoft.appcenter:espresso-test-extension:1.3'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'junit:junit:4.12'
}

repositories {
    mavenLocal()
    mavenCentral()
}