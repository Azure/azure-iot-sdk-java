apply plugin: 'com.android.application'

//***********************************************************************************************//
def IOTHUB_CONNECTION_STRING = project.hasProperty('IOTHUB_CONNECTION_STRING') ? '"'+project.property('IOTHUB_CONNECTION_STRING')+'"' : '"Define IotHub Connection String"'
def EVENT_HUB_CONNECTION_STRING = project.hasProperty('EVENT_HUB_CONNECTION_STRING') ? '"'+project.property('EVENT_HUB_CONNECTION_STRING')+'"': '"Define Event Hub Connection String"'
def APPCENTER_APP_SECRET = project.hasProperty('AppCenterAppSecret') ? '"'+project.property('AppCenterAppSecret')+'"': '"Define AppCenter App Secret"'

def STRING='String'
//***********************************************************************************************//

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.microsoft.azure.sdk.iot.android.digitaltwin"
        minSdkVersion 24
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //********** We can define variables here **********
        each {
            //buildTypes.mBuildConfigFields 'DATATYPE','VARIABLE',|"GRADLE VARIABLE|"'
            buildConfigField STRING, 'IOTHUB_CONNECTION_STRING', IOTHUB_CONNECTION_STRING
            buildConfigField STRING, 'EVENT_HUB_CONNECTION_STRING', EVENT_HUB_CONNECTION_STRING
            buildConfigField STRING, 'AppCenterAppSecret', APPCENTER_APP_SECRET
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    packagingOptions {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'thirdpartynotice.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions{
        ignore 'InvalidPackage'
    }
}

dependencies {
    def appCenterSdkVersion = '1.8.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:multidex:1.0.3'

    implementation('com.microsoft.azure.sdk.iot:digital-twin-e2e-tests-common:1.0.0'){
        exclude module: 'junit'
        exclude module: 'azure-storage'
    }
    implementation ('com.microsoft.azure.android:azure-storage-android:2.0.0@aar')
    implementation ('org.apache.commons:commons-lang3:3.6')

    androidTestImplementation 'com.microsoft.appcenter:espresso-test-extension:1.3'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'junit:junit:4.12'

    // Adds libraries defining annotations to only the compile classpath.
    implementation 'com.microsoft.azure:azure-annotations:1.7.0'
    // Adds the annotation processor dependency to the annotation processor classpath.
    annotationProcessor 'com.microsoft.azure:azure-annotations:1.7.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}